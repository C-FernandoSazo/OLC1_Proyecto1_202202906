package Analizadores;
import java_cup.runtime.*;
import java.util.HashMap;
import Analizadores.Objetos.ConEjecucion;
import Analizadores.Objetos.Errores;
import Analizadores.Objetos.Variable;
import java.util.ArrayList;
import java.util.Arrays;
import Operaciones.Graficar;
import java.io.File;
import java.io.IOException;

parser code 
{:
    public void syntax_error(Symbol s){
        ConEjecucion.errores.add(new Errores("SINTACTICO", s.left, s.right, s.value.toString()));   
        System.out.println("Error sintáctico: "+s.value+" linea: "+s.left+" columna: "+s.right);
    }
    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{
        System.out.println("Error sintáctico unrecovered: "+s.value+" linea: "+s.left+" columna: "+s.right);
    }
:}

terminal IDENTIFICADOR, NUMERO, CADENA, DECIMAL;
terminal VAR, PUNTO_Y_COMA, PUNTOS, DOS_PUNTOS, DOUBLE, CHAR, PROGRAM, END, ASIGNACION, ACCESO;
terminal COMA, ARROBA, ARR, OPENCORCHETE, CLOSECORCHETE, OPENPAREN, CLOSEPAREN, IGUAL, CONSOLE, PRINT, COLUMN;
terminal SUMA, RESTA, MULT, DIV, MOD, MEDIA, MEDIANA, MODA, VARIANZA, MAX, MIN;
terminal GBAR, GPIE, GLINE, GHISTOGRAM, TITULO, EJEX, EJEY, TITULOX, TITULOY, VALUES, LABEL, EXEC;

non terminal inicio, declaracion_variable, declaraciones, sentencia, valor, declaracion_array, lista_valoresDouble, operacion_arit;
non terminal arreglo_double, valor_double, valor_string, sentencia_print, lista_expresiones, funcion_estadistica;
non terminal lista_valoresChar, sentencia_column, lista_expresionesColumn, variable, lista_valores, arreglo_char;
non terminal declaracion_grafico, sentencia_grafico, atributosGraph;

//Estructura principal del programa

start with inicio;

inicio ::= PROGRAM declaraciones END PROGRAM
{:
    ConEjecucion.imprimirVariables();
:}
;

declaraciones ::= declaraciones sentencia
                | sentencia
;

sentencia ::= declaracion_variable
                | sentencia_print
                | declaracion_array
                | sentencia_column
                | declaracion_grafico
;

declaracion_variable ::= VAR PUNTOS DOUBLE DOS_PUNTOS IDENTIFICADOR:id ASIGNACION valor_double:v END PUNTO_Y_COMA
                {: 
                    Variable var = new Variable("double",Double.valueOf(v.toString()));
                    ConEjecucion.variables.put(id.toString(), var); 
                :}   
                | VAR PUNTOS CHAR DOS_PUNTOS IDENTIFICADOR:id ASIGNACION valor_string:v END PUNTO_Y_COMA
               {: 
                    Variable var = new Variable("string",v.toString());
                    ConEjecucion.variables.put(id.toString(), var); 
                :}
;

declaracion_array ::= ARR PUNTOS DOUBLE DOS_PUNTOS ARROBA IDENTIFICADOR:id ASIGNACION OPENCORCHETE 
                        lista_valoresDouble:lista CLOSECORCHETE END PUNTO_Y_COMA
                {:
                    // Casting seguro a ArrayList<Double>
                    ArrayList<Double> listaActual;
                    if (lista instanceof ArrayList) {
                        listaActual = (ArrayList<Double>) lista;
                    } else {
                        // Manejo de error o inicialización alternativa
                        listaActual = new ArrayList<Double>();
                    }
                    double[] array = new double[listaActual.size()];
                    for (int i = 0; i < listaActual.size(); i++) {
                        array[i] = listaActual.get(i);
                    }
                    ConEjecucion.arrays.put(id.toString(), array);
                :}
    | ARR PUNTOS CHAR DOS_PUNTOS ARROBA IDENTIFICADOR:id ASIGNACION OPENCORCHETE 
        lista_valoresChar:lista CLOSECORCHETE END PUNTO_Y_COMA
                {:
                    ArrayList<String> listaActual;
                    if (lista instanceof ArrayList) {
                        listaActual = (ArrayList<String>) lista;
                    } else {
                        // Manejo de error o inicialización alternativa
                        listaActual = new ArrayList<String>();
                    }
                    String[] array = new String[listaActual.size()];
                    for (int i = 0; i < listaActual.size(); i++) {
                        array[i] = listaActual.get(i);
                    }
                    ConEjecucion.arrayChar.put(id.toString(), array);                    
                :}
;

lista_valores ::= lista_valores:lista COMA valor:var {:
                    ArrayList<Object> listaActual = (ArrayList<Object>) lista;
                    listaActual.add(var);
                    RESULT = listaActual;
                :}
                | valor:var {:
                    ArrayList<Object> nuevaLista = new ArrayList<Object>();
                    nuevaLista.add(var);
                    RESULT = nuevaLista;
                :}
;

lista_valoresDouble ::= lista_valoresDouble:lista COMA valor:var {:
                    ArrayList<Double> listaActual = (ArrayList<Double>) lista;
                    listaActual.add((Double) var); 
                    RESULT = listaActual; 
                :}
                | valor:var 
                {:
                    ArrayList<Double> nuevaLista = new ArrayList<Double>();
                    nuevaLista.add((Double) var);
                    RESULT = nuevaLista;
                :}
;

lista_valoresChar ::= lista_valoresChar:lista COMA valor:var {:
                    ArrayList<String> listaActual = (ArrayList<String>) lista;
                    listaActual.add((String) var); 
                    RESULT = listaActual; 
                :}
                | valor:var 
                {:
                    ArrayList<String> nuevaLista = new ArrayList<String>();
                    nuevaLista.add((String) var);
                    RESULT = nuevaLista;
                :}
;

sentencia_print ::= CONSOLE DOS_PUNTOS PRINT IGUAL lista_expresiones:text END PUNTO_Y_COMA {:
                    System.out.println("Salida: "+text.toString());
                :}
;

lista_expresiones ::= lista_expresiones:text1 COMA valor:text2 {:
                        RESULT = text1.toString() + ", " + text2.toString();
                        :}
                    | valor:text   {: RESULT = text.toString(); :}
;

sentencia_column ::= CONSOLE DOS_PUNTOS COLUMN IGUAL valor:var ACCESO lista_expresionesColumn:arreglo END PUNTO_Y_COMA  {:
                            System.out.println("----------");
                            System.out.println(var.toString());
                            System.out.println("----------");
                            if (arreglo instanceof double[]){
                                double[] temp = (double[]) arreglo;
                                for(double elemento : temp) {
                                    System.out.println(elemento);
                                }                                 
                            } else{ 
                                Object[] array = (Object[]) arreglo;
                                for(Object elemento : array) {
                                    System.out.println(elemento);
                                } 
                            }  
                        :}
;


lista_expresionesColumn ::= ARROBA IDENTIFICADOR:id     {:
                            double[] nums = ConEjecucion.arrays.get(id.toString());
                            String [] text = ConEjecucion.arrayChar.get(id.toString());
                            if(nums != null){
                                RESULT = nums;
                            } else if(text != null) {
                                RESULT = text;
                            }
                        :}
                        | OPENCORCHETE lista_valores:lista CLOSECORCHETE {:
                            ArrayList<Object> listaActual;
                            if (lista instanceof ArrayList) {
                                listaActual = (ArrayList<Object>) lista;
                            } else {
                                // Manejo de error o inicialización alternativa
                                listaActual = new ArrayList<Object>();
                            }
                            Object[] array = new Object[listaActual.size()];
                            for (int i = 0; i < listaActual.size(); i++) {
                                array[i] = listaActual.get(i);
                            }
                            RESULT = array;                
                        :}
;

operacion_arit ::=
      SUMA OPENPAREN valor:num1 COMA valor:num2 CLOSEPAREN 
      {: 
          RESULT = Double.valueOf(((Double)num1).doubleValue() + ((Double)num2).doubleValue()); 
      :}
    | RESTA OPENPAREN valor:num1 COMA valor:num2 CLOSEPAREN 
      {: 
          RESULT = Double.valueOf(((Double)num1).doubleValue() - ((Double)num2).doubleValue()); 
      :}
    | MULT OPENPAREN valor:num1 COMA valor:num2 CLOSEPAREN 
      {: 
          RESULT = Double.valueOf(((Double)num1).doubleValue() * ((Double)num2).doubleValue());
      :}
    | DIV OPENPAREN valor:num1 COMA valor:num2 CLOSEPAREN 
      {: 
          RESULT = Double.valueOf(((Double)num1).doubleValue() / ((Double)num2).doubleValue()); 
      :}
    | MOD OPENPAREN valor:num1 COMA valor:num2 CLOSEPAREN 
      {: 
          RESULT = Double.valueOf(((Double)num1).doubleValue() % ((Double)num2).doubleValue()); 
      :}
;


funcion_estadistica ::= MEDIA OPENPAREN arreglo_double:arreglo CLOSEPAREN 
                    {:
                        double[] array = (double[]) arreglo;
                        double media = 0;
                        for (int i=0; i < array.length; i++) {
                            media += array[i];
                        }
                        media = media / array.length;
                        RESULT = media;
                    :}
                 | MEDIANA OPENPAREN arreglo_double:arreglo CLOSEPAREN
                    {:
                        double[] array = (double[]) arreglo;
                        Arrays.sort(array);
                        int n = array.length;
                        double mediana;
                        if (n % 2 != 0) {
                            mediana =  array[n/2];
                            RESULT = mediana;
                        } else {
                                mediana = (array[(n/2)-1] + array[n/2]) / 2;
                                RESULT = mediana;
                            }
                    :}
                 | MODA OPENPAREN arreglo_double:arreglo CLOSEPAREN
                    {:
                        double[] array = (double[]) arreglo;
                        int maxRepeticion = 0;
                        double moda = 0;
                        for (int i=0; i < array.length; i++) {
                            int numRepeticiones = 0;
                            for (int j=0; j<array.length; j++) {
                                if(array[i] == array[j]) {
                                    numRepeticiones++;
                                }
                                if (numRepeticiones>maxRepeticion) {    
                                    moda = array[i];
                                    maxRepeticion = numRepeticiones;
                                }
                            }
                        }
                        RESULT = moda;
                    :}
                 | VARIANZA OPENPAREN arreglo_double:arreglo CLOSEPAREN
                    {:
                        double[] array = (double[]) arreglo;
                        double media = 0;
                        for (int i=0; i < array.length; i++) {
                            media += array[i];
                        }
                        media = media / array.length;

                        double sumaCuadrados = 0;
                        double varianza = 0;
                        for (double num : array){
                            sumaCuadrados += Math.pow(num - media, 2);
                        }
                        varianza = sumaCuadrados / array.length;
                        RESULT = varianza;
                    :}
                 | MAX OPENPAREN arreglo_double:arreglo CLOSEPAREN
                    {:
                        double[] array = (double[]) arreglo;
                        double max = 0;
                        for (int i = 0; i<array.length; i++) {
                            if (array[i] > max) {
                                max = array[i];
                            }
                        }
                        RESULT = max;
                    :}
                 | MIN OPENPAREN arreglo_double:arreglo CLOSEPAREN
                    {:
                        double[] array = (double[]) arreglo;
                        double min = 0;
                        for (int i = 0; i<array.length; i++) {
                            if (min == 0){
                                min = array[i];
                            }
                            else if (array[i] < min) {
                                min = array[i];
                            }
                        }
                        RESULT = min;
                    :}
                 ;

arreglo_double ::= OPENCORCHETE lista_valoresDouble:lista CLOSECORCHETE {:
                    ArrayList<Double> listaActual;
                    if (lista instanceof ArrayList) {
                        listaActual = (ArrayList<Double>) lista;
                    } else {
                        // Manejo de error o inicialización alternativa
                        listaActual = new ArrayList<Double>();
                    }
                    double[] array = new double[listaActual.size()];
                    for (int i = 0; i < listaActual.size(); i++) {
                        array[i] = listaActual.get(i);
                    }
                    RESULT = array;                
                :}
                 | ARROBA IDENTIFICADOR:id  {:
                    double[] array = ConEjecucion.arrays.get(id.toString());
                    if (array != null) {
                        RESULT = array;
                    } else {
                            throw new Exception("Array no definido: " + id.toString());
                        }
                 :}
;

arreglo_char ::= OPENCORCHETE lista_valoresChar:lista CLOSECORCHETE {:
                    ArrayList<String> listaActual;
                    if (lista instanceof ArrayList) {
                        listaActual = (ArrayList<String>) lista;
                    } else {
                        // Manejo de error o inicialización alternativa
                        listaActual = new ArrayList<String>();
                    }
                    String[] array = new String[listaActual.size()];
                    for (int i = 0; i < listaActual.size(); i++) {
                        array[i] = listaActual.get(i);
                    }
                    RESULT = array;                
                :}
                 | ARROBA IDENTIFICADOR:id  {:
                    String[] array = ConEjecucion.arrayChar.get(id.toString());
                    if (array != null) {
                        RESULT = array;
                    } else {
                            throw new Exception("Array no definido: " + id.toString());
                        }
                 :}
;

valor ::= |valor_double:n {:
                    RESULT = Double.valueOf(n.toString()); 
                    :}
        | valor_string:c  {:
                    RESULT = c.toString();
                    :}
        | variable:var  {:
                    RESULT = var;
                    :}
;

valor_double ::= NUMERO:n   {: 
                    RESULT = Double.valueOf(n.toString()); 
                :}
                | DECIMAL:d  {:
                    RESULT = Double.valueOf(d.toString());
                :}
                | operacion_arit:op {: RESULT = op; :}
                | funcion_estadistica:est {: RESULT = est; :}
;

valor_string ::= CADENA:c   {:
                    RESULT = c.toString();
                    :}
;

variable ::= IDENTIFICADOR:id   {:
                Variable var = ConEjecucion.variables.get(id.toString());
                if (var != null) {
                    RESULT = var.getValor();
                } else {
                    System.out.println("Variable no encontrada");
                }
            :}
;

declaracion_grafico ::= GBAR OPENPAREN sentencia_grafico EXEC GBAR END PUNTO_Y_COMA CLOSEPAREN END PUNTO_Y_COMA {:
                        String titulo = ConEjecucion.atributesGraph.get("Titulo").toString();
                        String [] ejex = (String[]) ConEjecucion.atributesGraph.get("ejex");
                        double [] ejey = (double[]) ConEjecucion.atributesGraph.get("ejey");
                        String titulox = ConEjecucion.atributesGraph.get("Titulox").toString();
                        String tituloy = ConEjecucion.atributesGraph.get("Tituloy").toString();
                        Graficar.grafica_barras(titulo, ejex, ejey, titulox, tituloy);
                    :}
                    | GPIE OPENPAREN sentencia_grafico EXEC GPIE END PUNTO_Y_COMA CLOSEPAREN END PUNTO_Y_COMA   {:
                        String titulo = ConEjecucion.atributesGraph.get("Titulo").toString();
                        double[] values = (double[]) ConEjecucion.atributesGraph.get("values");
                        String [] labels = (String[]) ConEjecucion.atributesGraph.get("labels");
                        Graficar.graficar_pie(titulo, values, labels);
                    :}
                    | GLINE OPENPAREN sentencia_grafico EXEC GLINE END PUNTO_Y_COMA CLOSEPAREN END PUNTO_Y_COMA {:
                        String titulo = ConEjecucion.atributesGraph.get("Titulo").toString();
                        String [] ejex = (String[]) ConEjecucion.atributesGraph.get("ejex");
                        double [] ejey = (double[]) ConEjecucion.atributesGraph.get("ejey");
                        String titulox = ConEjecucion.atributesGraph.get("Titulox").toString();
                        String tituloy = ConEjecucion.atributesGraph.get("Tituloy").toString();
                        Graficar.grafica_linea(titulo, ejex, ejey, titulox, tituloy);
                    :}
                    | GHISTOGRAM OPENPAREN sentencia_grafico EXEC GHISTOGRAM END PUNTO_Y_COMA CLOSEPAREN END PUNTO_Y_COMA   {:
                        String titulo = ConEjecucion.atributesGraph.get("Titulo").toString();   
                        double[] values = (double[]) ConEjecucion.atributesGraph.get("values");
                    :}
;

sentencia_grafico ::= sentencia_grafico atributosGraph
                    | atributosGraph
;

atributosGraph ::= TITULO DOS_PUNTOS CHAR IGUAL valor:titulo END PUNTO_Y_COMA
                {: ConEjecucion.atributesGraph.put("Titulo",titulo.toString());  :}
                | EJEX DOS_PUNTOS CHAR IGUAL arreglo_char:ejex END PUNTO_Y_COMA
                {: String[] array = (String[]) ejex;
                    ConEjecucion.atributesGraph.put("ejex",array); :}
                | EJEY DOS_PUNTOS DOUBLE IGUAL arreglo_double:ejey END PUNTO_Y_COMA
                {: double[] array = (double[]) ejey;
                    ConEjecucion.atributesGraph.put("ejey",array);  :}
                | TITULOX DOS_PUNTOS CHAR IGUAL valor:titulox END PUNTO_Y_COMA
                {: ConEjecucion.atributesGraph.put("Titulox",titulox.toString());  :}
                | TITULOY DOS_PUNTOS CHAR IGUAL valor:tituloy END PUNTO_Y_COMA
                {: ConEjecucion.atributesGraph.put("Tituloy",tituloy.toString());  :}
                | LABEL DOS_PUNTOS CHAR IGUAL arreglo_char:labels END PUNTO_Y_COMA
                {: String[] array = (String[]) labels;
                    ConEjecucion.atributesGraph.put("labels",array); :}
                | VALUES DOS_PUNTOS DOUBLE IGUAL arreglo_double:values END PUNTO_Y_COMA
                {: double[] array = (double[]) values;
                    ConEjecucion.atributesGraph.put("values",array);  :}
;